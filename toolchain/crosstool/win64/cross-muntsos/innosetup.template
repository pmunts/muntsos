; Copyright (C)2025, Philip Munts, President, Munts AM Corp.
;
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions are met:
;
; * Redistributions of source code must retain the above copyright notice,
;   this list of conditions and the following disclaimer.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
; POSSIBILITY OF SUCH DAMAGE.

[Setup]
MinVersion=10.0
AppName=@@APPNAME@@
AppVersion={#APPVERSION}
AppPublisher=Munts Technologies
AppPublisherURL=http://tech.munts.com
DefaultDirName={commonpf64}\@@APPNAME@@
DirExistsWarning=no
Compression=lzma2
SolidCompression=yes
InfoBeforeFile=innosetup.txt
OutputBaseFileName=@@APPNAME@@-{#APPVERSION}
outputDir=.
UninstallFilesDir={app}/uninstall

[Code]
var CancelWithoutPrompt : Boolean;

function InitializeSetup(): Boolean;

begin
  CancelWithoutPrompt := false;
  Result := true
end;

procedure CancelButtonClick(CurPageID: Integer; var Cancel, Confirm: Boolean);

begin
  Confirm := not CancelWithoutPrompt;
end;

procedure AbortInstallation(reason : String);

begin
  MsgBox(reason, mbError, MB_OK);
  CancelWithoutPrompt := true;
  WizardForm.Close;
end;

procedure CurStepChanged(CurStep: TSetupStep);

var
  resultcode : Integer;

begin
  if CurStep <> ssInstall then
    Exit;

  if not Exec(GetEnv('windir') + '\System32\cmd.exe',
    '/C mkdir C:\PROGRA~1\@@APPNAME@@',
    '', SW_HIDE, ewWaitUntilTerminated, resultcode) then
    AbortInstallation('mkdir failed.  Installation cannot continue.');

  if not Exec(GetEnv('windir') + '\System32\fsutil.exe',
    'file setCaseSensitiveInfo C:\PROGRA~1\@@APPNAME@@ enable',
    '', SW_HIDE, ewWaitUntilTerminated, resultcode) then
    AbortInstallation('fsutil failed.  Installation cannot continue.');
end;

[Files]
Source: "@@APPNAME@@/*"; Flags: recursesubdirs createallsubdirs; DestDir: "{app}"
